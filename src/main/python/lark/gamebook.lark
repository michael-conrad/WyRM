// game book

start: metadata rooms

_eos: ";" _ws
_nl: NEWLINE
_ws: WS*

?metadata: metadata_entries
?metadata_entries: metadata_entry*
metadata_entry: metadata_tag ":" metadata_value _nl
?metadata_tag: IDENTIFIER
?metadata_value: REMAINING_LINE?

rooms: room statement+
room: "[" ROOM_NAME "]" _nl

ROOM_NAME: /[^]]+/i

direction_statement: "direction" short_format_string direction_room direction_facing _eos

direction_room: STRING
short_format_string: STRING

direction_facing: ("facing" STRING)
    | "facing" "(" expression ")" -> facing_expr

option_statement: "option" short_format_string compound_statement

// run once, first time run, further runs are a noop
once_statement: "once" compound_statement | "once" compound_statement once_else compound_statement
once_else: "else"

// partial grammar taken from https://raw.githubusercontent.com/igordejanovic/parglare/master/examples/c/c2.pg

?statement: room
    | compound_statement
    | expression_statement
    | selection_statement
    | iteration_statement
    | jump_statement
    | direction_statement
    | once_statement
    | assignment_statement
    | option_statement
    | go_statement
    | empty_statement
    | scope_statement
    | comment_statement
    | start_over
    | macro_define
    | macro_invoke

macro_define: "macro" macro_id compound_statement
macro_invoke: "macro" macro_id _eos
macro_id: IDENTIFIER

start_over: "start" "over" _eos

scope_statement: (WORLD | ROOM | LOCAL | EXTERN) IDENTIFIER _eos


WORLD: "world"
ROOM: "room"
LOCAL: "local"
EXTERN: "extern"


go_statement: "go" next_room _eos
next_room: STRING

?compound_statement: "{" _nl? "}" _nl? | "{" _nl?  block_item_list "}" _nl?
?block_item_list: block_item | block_item_list block_item
?block_item: declaration | statement

type_name: specifier_qualifier_list abstract_declarator
    | specifier_qualifier_list

abstract_declarator: pointer direct_abstract_declarator
    | pointer
    | direct_abstract_declarator

specifier_qualifier_list: type_specifier specifier_qualifier_list
    | type_specifier
    | type_qualifier specifier_qualifier_list
    | type_qualifier
    
direct_abstract_declarator: "(" abstract_declarator ")"
    | "[" "]"
    | "[" "*" "]"
    | "[" STATIC type_qualifier_list assignment_expression "]"
    | "[" STATIC assignment_expression "]"
    | "[" type_qualifier_list STATIC assignment_expression "]"
    | "[" type_qualifier_list assignment_expression "]"
    | "[" type_qualifier_list "]"
    | "[" assignment_expression "]"
    | direct_abstract_declarator "[" "]"
    | direct_abstract_declarator "[" "*" "]"
    | direct_abstract_declarator "[" STATIC type_qualifier_list assignment_expression "]"
    | direct_abstract_declarator "[" STATIC assignment_expression "]"
    | direct_abstract_declarator "[" type_qualifier_list assignment_expression "]"
    | direct_abstract_declarator "[" type_qualifier_list STATIC assignment_expression "]"
    | direct_abstract_declarator "[" type_qualifier_list "]"
    | direct_abstract_declarator "[" assignment_expression "]"
    | "(" ")"
    | "(" parameter_type_list ")"
    | direct_abstract_declarator "(" ")"
    | direct_abstract_declarator "(" parameter_type_list ")"

pointer: "*" type_qualifier_list pointer
    | "*" type_qualifier_list
    | "*" pointer
    | "*"

type_qualifier_list: type_qualifier
    | type_qualifier_list type_qualifier
    
parameter_type_list: parameter_list "," ELLIPSIS
    | parameter_list


parameter_list: parameter_declaration
    | parameter_list "," parameter_declaration


parameter_declaration: declaration_specifiers declarator
    | declaration_specifiers abstract_declarator
    | declaration_specifiers


identifier_list: IDENTIFIER
    | identifier_list "," IDENTIFIER


type_specifier: WORLD | ROOM | LOCAL | EXTERN | enum_specifier

type_qualifier: CONST

?expression_statement: expression _eos | string_constant _nl

empty_statement: _eos+

selection_statement: if_else_statement
    | if_statement
    | switch_statement

if_else_statement: IF "(" expression ")" statement ELSE statement
if_statement: IF "(" expression ")" statement
switch_statement: SWITCH "(" expression ")" statement

iteration_statement: while_statement | do_while_statement | for_each_statement | for_statement | repeat_statement


 // WHILE "(" expression ")" statement
    //| DO statement WHILE "(" expression ")" _eos
    //| FOR "(" expression_statement expression_statement ")" statement
    //| FOR "(" expression_statement expression_statement expression ")" statement
    //| FOR "(" declaration expression_statement ")" statement
    //| FOR "(" declaration expression_statement expression ")" statement

while_statement:  WHILE "(" expression ")" statement
do_while_statement: DO statement WHILE "(" expression ")" _eos
for_statement: FOR "(" assignment_expression ";" expression ";" expression ")" statement
for_each_statement: FOR "(" primary_expression ":" expression")" statement
repeat_statement: REPEAT expression statement

REPEAT: "repeat"
    
jump_statement: GOTO expression direction_facing _eos -> goto_statement
    | CONTINUE _eos
    | BREAK _eos
    | RETURN _eos
    | RETURN expression _eos

?cast_expression: unary_expression
    | dice_roll | max_dice_roll | min_dice_roll
    | python_list | python_tuple

python_list: "list" param_void | "list" param_args
python_tuple: "tuple" param_void | "tuple" param_args

dice_roll: unary_expression "@" unary_expression dice_explode
dice_explode: X?
X: "x"

max_dice_roll: "maxof" unary_expression "@" unary_expression dice_explode
min_dice_roll: "minof" unary_expression "@" unary_expression dice_explode

?multiplicative_expression: cast_expression
    | multiplicative_expression "*" cast_expression -> multiply
    | multiplicative_expression "/" cast_expression -> divide
    | multiplicative_expression "%" cast_expression -> modulo
    | multiplicative_expression "//" cast_expression -> int_divide

?additive_expression: multiplicative_expression
    | additive_expression "+" multiplicative_expression -> addition
    | additive_expression "-" multiplicative_expression -> subtraction

?shift_expression: additive_expression

?relational_expression: shift_expression
    | relational_expression LT_OP shift_expression -> is_lt
    | relational_expression GT_OP shift_expression -> is_gt
    | relational_expression LE_OP shift_expression -> is_lte
    | relational_expression GE_OP shift_expression -> is_gte

?equality_expression: relational_expression
    | equality_expression EQ_OP relational_expression -> is_eq
    | equality_expression (NE_OP|NE_OP2) relational_expression -> is_not_eq

?and_expression: equality_expression

?exclusive_or_expression: and_expression

?inclusive_or_expression: exclusive_or_expression

?logical_and_expression: inclusive_or_expression
    | logical_and_expression and_op inclusive_or_expression -> logical_and

and_op: AND_OP | AND_OP2

?logical_or_expression: logical_and_expression
    | logical_or_expression or_op logical_and_expression -> logical_or

or_op: OR_OP | OR_OP2

//ternary

conditional_expression: logical_or_expression | ternary_expression

ternary_expression: logical_or_expression "?" expression ":" conditional_expression

assignment_expression: conditional_expression
    | unary_expression assignment_operator assignment_expression

assignment_statement: unary_expression assignment_operator assignment_expression _eos
assignment_operator: ASSIGN
    | MUL_ASSIGN
    | DIV_ASSIGN
    | MOD_ASSIGN
    | ADD_ASSIGN
    | SUB_ASSIGN

?expression: assignment_expression
    // | expression "," assignment_expression
    // | "[" expression "]"

constant_expression: conditional_expression    // with constraints

declaration: declaration_specifiers _eos

declaration_specifiers: type_specifier IDENTIFIER
    | type_qualifier declaration_specifiers

declarator: pointer direct_declarator
    | direct_declarator
    
enum_specifier: ENUM "{" enumerator_list "}"
    | ENUM "{" enumerator_list "," "}"
    | ENUM IDENTIFIER "{" enumerator_list "}"
    | ENUM IDENTIFIER "{" enumerator_list "," "}"
    | ENUM IDENTIFIER
    
?unary_expression: postfix_expression
    | unary_operator cast_expression -> unary_cast

unary_operator: "+" -> expression_positive
    | "-" -> expression_negate
    | "!" -> logical_not
    | "not" -> logical_not

direct_declarator: IDENTIFIER
    | "(" declarator ")"
    | direct_declarator "[" "]"
    | direct_declarator "(" parameter_type_list ")"
    | direct_declarator "(" ")"
    | direct_declarator "(" identifier_list ")"

enumerator_list: enumerator
    | enumerator_list "," enumerator


// identifiers must be flagged as ENUMERATION_CONSTANT
enumerator: enumeration_constant "=" constant_expression
    | enumeration_constant

postfix_expression: primary_expression
    | postfix_array
    | postfix_function_void
    | postfix_function_params
    | postfix_dot

?postfix_dot: postfix_expression DOT IDENTIFIER
postfix_array: postfix_expression lbracket expression rbracket

lbracket: "["
rbracket: "]"

postfix_function_void: postfix_expression param_void
postfix_function_params: postfix_expression param_args

param_void: "(" ")"
param_args: "(" argument_expression_list ")"

?assign_identifier: postfix_expression "[" expression "]"
    | postfix_expression DOT IDENTIFIER

init_declarator: declarator "=" initializer
    | declarator

// before it has been defined as such
enumeration_constant: IDENTIFIER

argument_expression_list: argument_expression_list comma expression
     | expression

comma: ","

primary_expression: constant
    | string_constant
    | "(" expression ")" -> paren_expression
    | IDENTIFIER

initializer: "{" initializer_list "}"
    | "{" initializer_list "," "}"
    | assignment_expression

?constant: I_CONSTANT        // includes character_constant
    | F_CONSTANT
    | boolean_value
    | NONE
    // | ENUMERATION_CONSTANT  // after it has been defined as such

NONE: "None"

boolean_value: boolean_true | boolean_false

boolean_true: TRUE
boolean_false: FALSE

TRUE: "true"
FALSE: "false"

string_constant: STRING | LONG_STRING

initializer_list: designation initializer
    | initializer
    | initializer_list "," designation initializer
    | initializer_list "," initializer

designation: designator_list "="

designator_list: designator
    | designator_list designator
    
designator: "[" constant_expression "]"
    | DOT IDENTIFIER

IDENTIFIER: ID
ENUMERATION_CONSTANT: ID

ID: /[a-zA-Z_][a-zA-Z_0-9]*/
DOT: "."

I_CONSTANT: /[1-9][0-9]*/ | "0"
F_CONSTANT: /[0-9]*"."[0-9]+/ | /[0-9]+"."/

LT_OP: "<"
GT_OP: ">"
LE_OP: "<="
GE_OP: ">="
EQ_OP: "=="
NE_OP: "!="
AND_OP: "&&"
OR_OP: "||"
NE_OP2: "is not"
AND_OP2: "and"
OR_OP2: "or"

ASSIGN: "="
MUL_ASSIGN: "*="
DIV_ASSIGN: "/="
MOD_ASSIGN: "%="
ADD_ASSIGN: "+="
SUB_ASSIGN: "-="

STATIC: "static"
CONST: "const"
ENUM: "enum"
ELLIPSIS: "..."

CASE: "case"
DEFAULT: "default"
IF: "if"
ELSE: "else"
SWITCH: "switch"
WHILE: "while"
DO: "do"
FOR: "for"
GOTO: "goto"
CONTINUE: "continue"
BREAK: "break"
RETURN: "return"

STRING: /"(?!"").*?(?<!\\)(\\\\)*?"/i
LONG_STRING: /""".*?(?<!\\)(\\\\)*?"""/is
REMAINING_LINE: /[^\n]+/

?comment_statement: short_comment | long_comment
short_comment: SHORT_COMMENT _nl
long_comment: LONG_COMMENT _nl*
SHORT_COMMENT: "//" /[^\n]+/
LONG_COMMENT: "/*" /.*?(?<!\\)(\\\\)*?/is "*/"

%import common.NEWLINE
%import unicode.WS_INLINE
%import unicode.WS

// %ignore NEWLINE
%ignore WS_INLINE
// %ignore /\\[\t \f]*\r?\n/   // LINE_CONT

